/*
 * Copyright (c) 2015 Jonas Kalderstam.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
    jcenter()
}

// Version number
def versionMajor = 5 // Major UI overhauls
def versionMinor = 7 // Some new functionality
def versionPatch = 1 // Bug fixes
def versionBuild = 3 // Bump for dogfood builds, public betas, etc.

// Version name from git
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return "GITFAILEDINGRADLE"
    }
}

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    namespace 'com.nononsenseapps.notepad' // for R and BuildConfig

    // dataBinding.enabled = true

    lint {
        htmlReport false
        textReport false
        abortOnError false
        explainIssues true
        checkTestSources true
        // turn off checking the given issue id's
        // disable += ['RtlHardcoded', 'MissingSuperCall']
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    // disable code shrinking in debug, enable it in release mode
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            pseudoLocalesEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        applicationId "com.nononsenseapps.notepad"
        minSdkVersion 23 // most people have at least android 7.1 anyway: https://apilevels.com/
        targetSdkVersion 32
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild * 10
        versionName getVersionName()
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    testOptions {
        // androidx espresso tests require this
        animationsDisabled true
    }

    compileOptions {
        // enable support for new language APIs in old devices
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    if (project.hasProperty('STORE_FILE')) {

        buildTypes {
            release {
            }
        }
    } else {
        println "No key store defined. Signed release not available..."
    }
}

dependencies {
    // AndroidX
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.cursoradapter:cursoradapter:1.0.0"
    implementation 'androidx.fragment:fragment:1.5.4'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.startup:startup-runtime:1.1.1" // it just fixes a build warning
    // Time library, open source & updated
    implementation 'joda-time:joda-time:2.12.0'
    // Dashclock API, open source & abandoned, but it still works
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    // OrgParser, open source & abandoned, but it still works
    implementation 'org.cowboyprogrammer:orgparser:1.3.1'
    // Http library for Google Task Sync, open source & abandoned, should upgrade to retrofit2
    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    // Included projects
    implementation project(':external:ActionBar-PullToRefresh')
    implementation project(':external:datetimepicker')
    implementation project(':external:drag-sort-listview')
    implementation project(':external:ShowcaseView')
    // annotations library, open source & abandoned
    annotationProcessor "org.androidannotations:androidannotations:4.8.0"
    implementation "org.androidannotations:androidannotations-api:4.8.0"
    // Tests libraries
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    // Desugaring library, needs android gradle plugin v7.4+ to upgrade
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'
}
