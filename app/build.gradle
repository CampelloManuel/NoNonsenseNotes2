/*
 * Copyright (c) 2015 Jonas Kalderstam.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
    // for TextDrawable
    maven { url 'https://jitpack.io' }
}

// Version number
def versionMajor = 7 // Major UI overhauls
def versionMinor = 0 // Some new functionality
def versionPatch = 0 // Bug fixes

// Version name from git
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return "GITFAILEDINGRADLE"
    }
}

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'
    namespace 'com.nononsenseapps.notepad' // for R and BuildConfig

    // dataBinding.enabled = true

    lint {
        htmlReport false
        textReport false
        abortOnError false
        explainIssues true
        checkTestSources true
        // turn off checking the given issue id's
        disable += ['RtlHardcoded', 'ApplySharedPref']
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    // disable code shrinking in debug, enable it in release mode
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            pseudoLocalesEnabled true
            // just to make gradle shut up about missing classes
            proguardFiles 'proguard.pro'
        }
        release {
            // enable R8
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
        }
    }

    defaultConfig {
        applicationId "com.nononsenseapps.notepad"
        minSdkVersion 23 // most people have at least android 7.1 anyway: https://apilevels.com/
        targetSdkVersion 32
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName getVersionName()
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    testOptions {
        // androidx espresso tests require this
        animationsDisabled true
    }

    compileOptions {
        // enable support for new language APIs in old devices
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    if (project.hasProperty('STORE_FILE')) {

        buildTypes {
            release {
            }
        }
    } else {
        // println "No key store defined. Signed release not available..."
    }
}

dependencies {
    // AndroidX
    implementation "androidx.cursoradapter:cursoradapter:1.0.0"
    implementation 'androidx.fragment:fragment:1.5.4'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.startup:startup-runtime:1.1.1" // it just fixes a build warning
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.7.0'
    // yes we need both. It solves a dependency problem with androidx.preference
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    // turns letters into drawables, open source & up to date
    implementation 'com.github.ramseth001:TextDrawable:1.1.6' // useless ??
    // Time library, open source & up to date
    implementation 'joda-time:joda-time:2.12.1'
    // Dashclock API, open source & abandoned, but it still works
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    // OrgParser, open source & abandoned, but it still works
    implementation 'org.cowboyprogrammer.orgparser:orgparser:1.3.1'
    // Http library for Google Task Sync, open source & abandoned, should upgrade to retrofit2
    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    // for manual list sorting, local source copy, open source & abandoned
    implementation project(':external:drag-sort-listview')
    // for highlighting functionality on 1Â° launch, open source & up to date
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    // annotations library, open source & abandoned
    annotationProcessor "org.androidannotations:androidannotations:4.8.0"
    implementation "org.androidannotations:androidannotations-api:4.8.0"
    // Tests libraries
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.0'
    // Desugaring library, needs android gradle plugin v7.4+ to upgrade
    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}
